%{
#include <stdio.h>
#include "../include/util.h"
#include "../include/errormsg.h"
#include "../include/symbol.h"
#include "../include/absyn.h"

#define YYDEBUG 1

int yylex(void);

A_exp absyn_root;

void yyerror(char *s)
{
	EM_error(EM_tokPos, "%s", s);
}

//int yydebug = 1;

%}

%union {
	int pos;
	int ival;
	string sval;
	A_var var;
	A_exp exp;
	A_expList expList;
	A_efieldList efieldList;
	A_dec dec;
	A_decList decList;
	A_nametyList nameTyList;
	A_fieldList fieldList;
	A_ty ty;
}

%token <sval> ID STRING
%token <ival> INT

%token
	COMMA COLON SEMICOLON LPAREN RPAREN LBRACK RBRACK
	LBRACE RBRACE DOT
	PLUS MINUS TIMES DIVIDE EQ NEQ LT LE GT GE
	AND OR ASSIGN
	ARRAY IF THEN ELSE WHILE FOR TO DO LET IN END OF
	BREAK NIL
	FUNCTION VAR TYPE

%left SEMICOLON
%left TIMES DIVIDE
%nonassoc EQ NEQ LT LE GT GE
%left AND OR
%left PLUS MINUS
%nonassoc THEN
%nonassoc ELSE

%type <var> lvalue_expression
%type <expList> argument_list
%type <efieldList> assignment_list
%type <decList> declarations
%type <fieldList> type_fields
%type <ty> type

%type <exp>
	program atom expression binop_expression
	expression_seq if_then_expression
	if_then_else_expression make_array_expression
	make_record_expression for_expression
	while_expression expression_kleenes
	let_declaration lvalue_assignment

%type <dec>
	declaration var_declaration
	func_declaration type_declaration

%start program

%%

program
	: expression
		{ absyn_root = $1; }

atom
	: INT
		{ $$ = A_IntExp(EM_tokPos, $1); }
	| MINUS atom
		{ $$ = A_OpExp(EM_tokPos, A_minusOp, A_IntExp(EM_tokPos, 0), $2); }
	| NIL
		{ $$ = A_NilExp(EM_tokPos); }
	| STRING
		{ $$ = A_StringExp(EM_tokPos, $1); }
	| BREAK
		{ $$ = A_BreakExp(EM_tokPos); }
	| LPAREN RPAREN
		{ $$ = A_NilExp(EM_tokPos); }
	| lvalue_expression
		{ $$ = A_VarExp(EM_tokPos, $1); }
	| LPAREN expression_seq RPAREN
		{ $$ = $2; }
	| LPAREN expression RPAREN
		{ $$ = $2; }
	| ID LPAREN RPAREN
		{ $$ = A_CallExp(EM_tokPos, S_Symbol($1), A_ExpList(NULL, NULL)); }
	| ID LPAREN argument_list RPAREN
		{ $$ = A_CallExp(EM_tokPos, S_Symbol($1), $3); }
	;

expression
	: atom
		{ $$ = $1; }
	| binop_expression
		{ $$ = $1; }
	| make_array_expression
		{ $$ = $1; }
	| make_record_expression
		{ $$ = $1; }
	| let_declaration
		{ $$ = $1; }
	| for_expression
		{ $$ = $1; }
	| while_expression
		{ $$ = $1; }
	| if_then_expression
		{ $$ = $1; }
	| if_then_else_expression
		{ $$ = $1; }
	| lvalue_assignment
		{ $$ = $1; }
	;

binop_expression
	: atom PLUS expression
		{ $$ = A_OpExp(EM_tokPos, A_plusOp, $1, $3); }
	| atom MINUS expression
		{ $$ = A_OpExp(EM_tokPos, A_minusOp, $1, $3); }
	| atom TIMES expression
		{ $$ = A_OpExp(EM_tokPos, A_timesOp, $1, $3); }
	| atom DIVIDE expression
		{ $$ = A_OpExp(EM_tokPos, A_divideOp, $1, $3); }
	| atom EQ expression
		{ $$ = A_OpExp(EM_tokPos, A_eqOp, $1, $3); }
	| atom NEQ expression
		{ $$ = A_OpExp(EM_tokPos, A_neqOp, $1, $3); }
	| atom LT expression
		{ $$ = A_OpExp(EM_tokPos, A_ltOp, $1, $3); }
	| atom LE expression
		{ $$ = A_OpExp(EM_tokPos, A_leOp, $1, $3); }
	| atom GT expression
		{ $$ = A_OpExp(EM_tokPos, A_gtOp, $1, $3); }
	| atom GE expression
		{ $$ = A_OpExp(EM_tokPos, A_geOp, $1, $3); }
	| atom AND expression
		{ $$ = A_IfExp(EM_tokPos, $1, $3, A_IntExp(EM_tokPos, 1)); }
	| atom OR expression
		{ $$ = A_IfExp(EM_tokPos, $1, A_IntExp(EM_tokPos, 1), $3); }
	;

make_array_expression
	: ID LBRACK expression RBRACK OF expression
		{ $$ = A_ArrayExp(EM_tokPos, S_Symbol($1), $3, $6); }
	;

make_record_expression
	: ID LBRACE RBRACE
		{ $$ = A_RecordExp(EM_tokPos, S_Symbol($1), A_EfieldList(NULL, NULL)); }
	| ID LBRACE assignment_list RBRACE
		{ $$ = A_RecordExp(EM_tokPos, S_Symbol($1), $3); }
	;

assignment_list
	: ID EQ expression
		{ $$ = A_EfieldList(A_Efield(S_Symbol($1), $3), NULL); }
	| assignment_list COMMA ID EQ expression
		{ $$ = A_EfieldList(A_Efield(S_Symbol($3), $5), $1); }
	;

argument_list
	: expression
		{ $$ = A_ExpList($1, NULL); }
	| argument_list COMMA expression
		{ $$ = A_ExpList($3, $1); }
	;

expression_seq
	: expression SEMICOLON expression
		{ $$ = A_SeqExp(EM_tokPos, A_ExpList($1, A_ExpList($3, NULL))); }
	| expression_seq SEMICOLON expression
		{ $$ = A_SeqExp(EM_tokPos, A_ExpList($3, $1->u.seq)); }
	;

let_declaration
	: LET declarations IN expression_kleenes END
		{ $$ = A_LetExp(EM_tokPos, $2, $4); }
	;

expression_kleenes
	: expression
		{ $$ = $1; }
	| expression_seq
		{ $$ = $1; }
	| /* NIL */
		{ $$ = A_NilExp(EM_tokPos); }
	;

declarations
	: declaration
		{ $$ = A_DecList($1, NULL); }
	| declaration declarations
		{ $$ = A_DecList($1, $2); }
	;

declaration
	: type_declaration
		{ $$ = $1; }
	| var_declaration
		{ $$ = $1; }
	| func_declaration
		{ $$ = $1; }
	;

type_declaration
	: TYPE ID EQ type
		{ $$ = A_TypeDec(EM_tokPos, A_NametyList(A_Namety(S_Symbol($2), $4), NULL)); }
	;

type
	: ID
		{ $$ = A_NameTy(EM_tokPos, S_Symbol($1)); }
	| LBRACE type_fields RBRACE
		{ $$ = A_RecordTy(EM_tokPos, $2); }
	| ARRAY OF ID
		{ $$ = A_ArrayTy(EM_tokPos, S_Symbol($3)); }
	;

type_fields
	: ID COLON ID
		{ $$ = A_FieldList(A_Field(EM_tokPos, S_Symbol($1), S_Symbol($3)), NULL); }
	| type_fields COMMA ID COLON ID
		{ $$ = A_FieldList(A_Field(EM_tokPos, S_Symbol($3), S_Symbol($5)), $1); }
	;

lvalue_expression
	: ID
		{ $$ = A_SimpleVar(EM_tokPos, S_Symbol($1)); }
	| lvalue_expression DOT ID
		{ $$ = A_FieldVar(EM_tokPos, $1, S_Symbol($3)); }
	| lvalue_expression LBRACK expression RBRACK
		{ $$ = A_SubscriptVar(EM_tokPos, $1, $3); }
	;

lvalue_assignment
	: lvalue_expression ASSIGN expression
		{ $$ = A_AssignExp(EM_tokPos, $1, $3); }
	;

var_declaration
	: VAR ID ASSIGN expression
		{ $$ = A_VarDec(EM_tokPos, S_Symbol($2), NULL, $4); }
	| VAR ID COLON ID ASSIGN expression
		{ $$ = A_VarDec(EM_tokPos, S_Symbol($2), S_Symbol($4), $6); }
	;

func_declaration
	: FUNCTION ID LPAREN RPAREN EQ expression
		{ $$ = A_FunctionDec(EM_tokPos, A_FundecList(A_Fundec(EM_tokPos, S_Symbol($2), NULL, NULL, $6), NULL)); }
	| FUNCTION ID LPAREN RPAREN COLON ID EQ expression
		{ $$ = A_FunctionDec(EM_tokPos, A_FundecList(A_Fundec(EM_tokPos, S_Symbol($2), NULL, S_Symbol($6), $8), NULL)); }
	| FUNCTION ID LPAREN type_fields RPAREN EQ expression
		{ $$ = A_FunctionDec(EM_tokPos, A_FundecList(A_Fundec(EM_tokPos, S_Symbol($2), $4, NULL, $7), NULL)); }
	| FUNCTION ID LPAREN type_fields RPAREN COLON ID EQ expression
		{ $$ = A_FunctionDec(EM_tokPos, A_FundecList(A_Fundec(EM_tokPos, S_Symbol($2), $4, S_Symbol($7), $9), NULL)); }
	;

for_expression
	: FOR ID ASSIGN expression TO expression DO expression
		{ $$ = A_ForExp(EM_tokPos, S_Symbol($2), $4, $6, $8); }
	;

while_expression
	: WHILE expression DO expression
		{ $$ = A_WhileExp(EM_tokPos, $2, $4); }
	;

if_then_expression
	: IF expression THEN expression
		{ $$ = A_IfExp(EM_tokPos, $2, $4, A_NilExp(EM_tokPos)); }
	;

if_then_else_expression
	: IF expression THEN expression ELSE expression
		{ $$ = A_IfExp(EM_tokPos, $2, $4, $6); }
	;
